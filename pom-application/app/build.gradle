plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    def keystorePropertiesFile = rootProject.file("keystore.properties")

    if (keystorePropertiesFile.exists()) {
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        signingConfigs {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    compileSdk 33

    defaultConfig {
        applicationId "net.dzikoysk.presenceofmind"
        minSdk 29
        targetSdk 33
        versionCode 10
        versionName "1.0.0-RC.11"
        archivesBaseName = "presence-of-mind-$versionName"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        if (keystorePropertiesFile.exists()) {
            signingConfig signingConfigs.release
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                '-opt-in=kotlin.RequiresOptIn',
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    dependenciesInfo {
        includeInApk true
        includeInBundle true
    }
    namespace 'net.dzikoysk.presenceofmind'
}

dependencies {
    implementation('org.burnoutcrew.composereorderable:reorderable:0.9.6')
    implementation("io.github.androidpoet:dropdown:1.0.1")
    implementation('io.github.vanpra.compose-material-dialogs:core:0.9.0')
    implementation("com.github.jeziellago:compose-markdown:0.2.9")
    implementation("com.github.tfaki:ComposableSweetToast:1.0.1")

    def landscapist_version = "2.1.3"
    implementation("com.github.skydoves:landscapist-glide:$landscapist_version")
    implementation("com.github.skydoves:landscapist-placeholder:$landscapist_version")
    implementation("com.github.skydoves:landscapist-animation:$landscapist_version")

    def appUpdate = "2.0.1"
    implementation("com.google.android.play:app-update-ktx:$appUpdate")

    def jackson = '2.14.2'
    implementation("com.fasterxml.jackson.core:jackson-databind:$jackson")
    implementation("com.fasterxml.jackson.core:jackson-core:$jackson")
    implementation("com.fasterxml.jackson.core:jackson-annotations:$jackson")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jackson")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jackson")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin:$jackson")

    def room_version = '2.5.0'
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    def compose_libs_version = '1.4.0-beta02'
    implementation "androidx.compose.ui:ui:$compose_libs_version"
    implementation "androidx.compose.ui:ui-util:$compose_libs_version"
    implementation "androidx.compose.material:material:$compose_libs_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_libs_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_libs_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_libs_version"

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.core:core-ktx:1.9.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}